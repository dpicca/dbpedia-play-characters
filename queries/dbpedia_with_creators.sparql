PREFIX dbc: <http://dbpedia.org/resource/Category:>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>

SELECT DISTINCT ?x ?author ?wikidata ?work
WHERE {
  {
     ?x a dbo:FictionalCharacter
  } UNION {
     ?x ^dbo:series [] # Literary series centered on a character e.g. Sherlock Holmes
  } UNION {
     ?x  dc:subject/skos:broader* dbc:Fictional_characters
  } UNION {
	 ?x a dbo:Person
	  ; dbo:creator [] # This property usually goes together with the category
  }

OPTIONAL { ?x dbo:creator ?author. }
OPTIONAL{
?x owl:sameAs ?wikidata.
FILTER( strstarts(STR(?wikidata), "http://wikidata.dbpedia.org/resource/"))
}
OPTIONAL{
{ ?x dbo:series ?work. }
UNION
{ ?x dbp:series ?work. }

}
}